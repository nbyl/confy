plugins {
    id 'com.google.cloud.tools.jib' version '2.5.0'
    id 'org.springframework.boot' version '2.3.2.RELEASE'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-validation'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.liquibase:liquibase-core'
    runtime 'org.hibernate:hibernate-java8'


    runtime 'org.springframework.boot:spring-boot-devtools'
    runtime 'org.springframework.data:spring-data-rest-hal-browser'
    runtime 'org.postgresql:postgresql'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.testcontainers:postgresql:1.14.3'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:3.4.1'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}
sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
    }
    test {
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty 'integrationTestBaseUrl', project.getProperty('integrationTestBaseUrl')

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

jib.to.image = 'nbyl/confy:' + version
